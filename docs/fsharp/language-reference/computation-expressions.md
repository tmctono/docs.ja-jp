---
title: コンピュテーション式
description: '制御フローの構造とバインディングを使用してシーケンス処理および結合できる、F # で計算を記述するための便利な構文を作成する方法について説明します。'
ms.date: 08/15/2020
f1_keywords:
- let!_FS
ms.openlocfilehash: 1649d8c57ea9e025d40ef6d39d92b96795964150
ms.sourcegitcommit: 9c45035b781caebc63ec8ecf912dc83fb6723b1f
ms.translationtype: MT
ms.contentlocale: ja-JP
ms.lasthandoff: 08/25/2020
ms.locfileid: "88812160"
---
# <a name="computation-expressions"></a>コンピュテーション式

F # のコンピュテーション式は、制御フローの構造とバインディングを使用して、シーケンス処理および結合できる計算を作成するための便利な構文を提供します。 計算式の種類によっては、monads、モノ id、monads トランスフォーマー、およびアプリケーションを表現する方法と考えることができます。 ただし、Haskell *のような* 他の言語とは異なり、それらは1つの抽象化に関連付けられておらず、マクロやその他の形式のメタプログラミングに依存せず、便利で状況依存の構文を実現できません。

## <a name="overview"></a>概要

計算には多くの形式が必要です。 最も一般的な計算形式は、簡単に理解して変更できるシングルスレッド実行です。 ただし、すべての形式の計算は、シングルスレッド実行と単純なものではありません。 次に例をいくつか示します。

- 非決定的な計算
- 非同期計算
- Effectful の計算
- 注釈の計算

一般に、アプリケーションの特定の部分で実行する必要がある *状況依存* の計算があります。 コンテキストに依存するコードを記述することは困難な場合があります。これは、特定のコンテキストの外では、抽象化を行わずに "リーク" の計算を簡単に行うことができるためです。 多くの場合、これらの抽象化は自分で記述することが困難です。そのため、F # には **コンピュテーション式**と呼ばれる汎用的な方法があります。

コンピュテーション式は、文脈に依存した計算をエンコードするための統一された構文と抽象化モデルを提供します。

すべての計算式は、 *ビルダー* 型によってサポートされます。 ビルダーの型は、コンピュテーション式で使用できる操作を定義します。 「 [コンピュテーション式の新しい型の作成](computation-expressions.md#creating-a-new-type-of-computation-expression)」を参照してください。これは、カスタム計算式の作成方法を示しています。

### <a name="syntax-overview"></a>構文の概要

すべての計算式の形式は次のとおりです。

```fsharp
builder-expr { cexper }
```

ここで、 `builder-expr` はコンピュテーション式を定義するビルダー型の名前、 `cexper` はコンピュテーション式の式本体です。 たとえば、 `async` コンピュテーション式のコードは次のようになります。

```fsharp
let fetchAndDownload url =
    async {
        let! data = downloadData url

        let processedData = processData data

        return processedData
    }
```

前の例で示したように、コンピュテーション式内で使用できる特殊な追加の構文があります。 コンピュテーション式では、次の式の形式を使用できます。

```fsharp
expr { let! ... }
expr { do! ... }
expr { yield ... }
expr { yield! ... }
expr { return ... }
expr { return! ... }
expr { match! ... }
```

これらのキーワードおよびその他の標準 F # キーワードは、バッキングビルダーの型で定義されている場合にのみ、コンピュテーション式で使用できます。 唯一の例外は `match!` 、を使用するための構文砂糖です `let!` 。その後、結果にパターン一致を指定します。

ビルダーの型は、コンピュテーション式のフラグメントの結合方法を制御する特殊なメソッドを定義するオブジェクトです。つまり、そのメソッドは、コンピュテーション式の動作を制御します。 ビルダークラスを記述するもう1つの方法は、ループやバインドなど、多くの F # コンストラクトの操作をカスタマイズできるということです。

### `let!`

キーワードは、 `let!` 別のコンピュテーション式への呼び出しの結果を名前にバインドします。

```fsharp
let doThingsAsync url =
    async {
        let! data = getDataAsync url
        ...
    }
```

を使用してコンピュテーション式に呼び出しをバインドすると `let` 、コンピュテーション式の結果は得られません。 代わりに、そのコンピュテーション式に対して、 *実現* されていない呼び出しの値をバインドします。 を使用し `let!` て、結果にバインドします。

`let!` は、ビルダー型のメンバーによって定義され `Bind(x, f)` ます。

### `do!`

`do!`キーワードは、 `unit` (ビルダーのメンバーによって定義された) like 型を返すコンピュテーション式を呼び出すためのものです `Zero` 。

```fsharp
let doThingsAsync data url =
    async {
        do! submitData data url
        ...
    }
```

[非同期ワークフロー](asynchronous-workflows.md)の場合、この型は `Async<unit>` です。 その他の計算式の場合、型はになる可能性があり `CExpType<unit>` ます。

`do!` は、ビルダー型のメンバーによって定義され `Bind(x, f)` ます。ここで、は `f` を生成 `unit` します。

### `yield`

`yield`キーワードは、計算式から値を返して、として使用できるようにするためのものです <xref:System.Collections.Generic.IEnumerable%601> 。

```fsharp
let squares =
    seq {
        for i in 1..10 do
            yield i * i
    }

for sq in squares do
    printfn "%d" sq
```

ほとんどの場合、呼び出し元は省略できます。 省略する最も一般的な方法 `yield` は、演算子を使用することです `->` 。

```fsharp
let squares =
    seq {
        for i in 1..10 -> i * i
    }

for sq in squares do
    printfn "%d" sq
```

多くの異なる値を生成する可能性がある複雑な式の場合、場合によってはキーワードを省略するだけで、次のことが可能です。

```fsharp
let weekdays includeWeekend =
    seq {
        "Monday"
        "Tuesday"
        "Wednesday"
        "Thursday"
        "Friday"
        if includeWeekend then
            "Saturday"
            "Sunday"
    }
```

[C# で yield キーワード](../../csharp/language-reference/keywords/yield.md)を使用する場合と同様に、コンピュテーション式の各要素は反復処理されるときに返されます。

`yield` は、ビルダーの型のメンバーによって定義され `Yield(x)` ます。ここで、 `x` は、返される項目です。

### `yield!`

`yield!`キーワードは、コンピュテーション式から値のコレクションをフラット化するためのものです。

```fsharp
let squares =
    seq {
        for i in 1..3 -> i * i
    }

let cubes =
    seq {
        for i in 1..3 -> i * i * i
    }

let squaresAndCubes =
    seq {
        yield! squares
        yield! cubes
    }

printfn "%A" squaresAndCubes // Prints - 1; 4; 9; 1; 8; 27
```

評価されると、によって呼び出されるコンピュテーション式の項目は1つずつ `yield!` 返され、結果がフラット化されます。

`yield!` は、ビルダーの型のメンバーによって定義され `YieldFrom(x)` ます。ここで、 `x` は値のコレクションです。

とは異なり `yield` 、 `yield!` 明示的に指定する必要があります。 計算式では、その動作は暗黙的ではありません。

### `return`

キーワードは、 `return` コンピュテーション式に対応する型の値をラップします。 を使用した計算式 `yield` とは別に、計算式を "完了" するために使用されます。

```fsharp
let req = // 'req' is of type is 'Async<data>'
    async {
        let! data = fetch url
        return data
    }

// 'result' is of type 'data'
let result = Async.RunSynchronously req
```

`return` は、ビルダーの型のメンバーによって定義され `Return(x)` ます。ここで、 `x` はラップする項目です。

### `return!`

キーワードは、 `return!` コンピュテーション式の値を認識し、その結果を、コンピュテーション式に対応する型にラップします。

```fsharp
let req = // 'req' is of type is 'Async<data>'
    async {
        return! fetch url
    }

// 'result' is of type 'data'
let result = Async.RunSynchronously req
```

`return!` は、ビルダーの型のメンバーによって定義され `ReturnFrom(x)` ます。ここで、 `x` は別のコンピュテーション式です。

### `match!`

`match!`キーワードを使用すると、別のコンピュテーション式への呼び出しをインライン化し、結果のパターンマッチを行うことができます。

```fsharp
let doThingsAsync url =
    async {
        match! callService url with
        | Some data -> ...
        | None -> ...
    }
```

でコンピュテーション式を呼び出すと `match!` 、のような呼び出しの結果が認識され `let!` ます。 これは、通常、結果が [省略可能](options.md)であるコンピュテーション式を呼び出すときに使用されます。

## <a name="built-in-computation-expressions"></a>組み込みのコンピュテーション式

F # コアライブラリには、 [シーケンス式](sequences.md)、 [非同期ワークフロー](asynchronous-workflows.md)、および [クエリ式](query-expressions.md)という3つの組み込みのコンピュテーション式が定義されています。

## <a name="creating-a-new-type-of-computation-expression"></a>新しい種類のコンピュテーション式の作成

ビルダークラスを作成し、クラスに特定の特別なメソッドを定義することで、独自のコンピュテーション式の特性を定義できます。 ビルダークラスでは、次の表に示すように、必要に応じてメソッドを定義できます。

次の表では、ワークフロービルダークラスで使用できるメソッドについて説明します。

|**方法**|**一般的な署名**|**説明**|
|----|----|----|
|`Bind`|`M<'T> * ('T -> M<'U>) -> M<'U>`|`let!`コンピュテーション式でおよびに対して呼び出され `do!` ます。|
|`Delay`|`(unit -> M<'T>) -> M<'T>`|計算式を関数としてラップします。|
|`Return`|`'T -> M<'T>`|コンピュテーション式でに対して呼び出され `return` ます。|
|`ReturnFrom`|`M<'T> -> M<'T>`|コンピュテーション式でに対して呼び出され `return!` ます。|
|`Run`|`M<'T> -> M<'T>` または<br /><br />`M<'T> -> 'T`|コンピュテーション式を実行します。|
|`Combine`|`M<'T> * M<'T> -> M<'T>` または<br /><br />`M<unit> * M<'T> -> M<'T>`|コンピュテーション式でシーケンス処理を行うために呼び出されます。|
|`For`|`seq<'T> * ('T -> M<'U>) -> M<'U>` または<br /><br />`seq<'T> * ('T -> M<'U>) -> seq<M<'U>>`|コンピュテーション式の式に対して呼び出され `for...do` ます。|
|`TryFinally`|`M<'T> * (unit -> unit) -> M<'T>`|コンピュテーション式の式に対して呼び出され `try...finally` ます。|
|`TryWith`|`M<'T> * (exn -> M<'T>) -> M<'T>`|コンピュテーション式の式に対して呼び出され `try...with` ます。|
|`Using`|`'T * ('T -> M<'U>) -> M<'U> when 'T :> IDisposable`|コンピュテーション式の束縛に対して呼び出され `use` ます。|
|`While`|`(unit -> bool) * M<'T> -> M<'T>`|コンピュテーション式の式に対して呼び出され `while...do` ます。|
|`Yield`|`'T -> M<'T>`|コンピュテーション式の式に対して呼び出され `yield` ます。|
|`YieldFrom`|`M<'T> -> M<'T>`|コンピュテーション式の式に対して呼び出され `yield!` ます。|
|`Zero`|`unit -> M<'T>`|コンピュテーション式の式の空の分岐に対して呼び出され `else` `if...then` ます。|
|`Quote`|`Quotations.Expr<'T> -> Quotations.Expr<'T>`|コンピュテーション式が `Run` メンバーに引用符として渡されることを示します。 計算のすべてのインスタンスを引用符に変換します。|

ビルダークラスのメソッドの多くは、構造体を使用して返します。 `M<'T>` 通常は、 `Async<'T>` 非同期ワークフローやシーケンスワークフローなど、結合する計算の種類を特徴付ける個別に定義された型です `Seq<'T>` 。 これらのメソッドのシグネチャを使用すると、1つのコンストラクトから返されるワークフローオブジェクトを次の構造体に渡すことができるように、それらを組み合わせて入れ子にすることができます。 コンパイラは、コンピュテーション式を解析するときに、前の表のメソッドとコンピュテーション式のコードを使用して、一連の入れ子になった関数呼び出しに式を変換します。

入れ子になった式の形式は次のとおりです。

```fsharp
builder.Run(builder.Delay(fun () -> {| cexpr |}))
```

上記のコードで `Run` `Delay` は、コンピュテーション式ビルダークラスでが定義されていない場合、およびの呼び出しは省略されます。 ここではと示されているコンピュテーション式の本体は、 `{| cexpr |}` 次の表で説明する変換によって、ビルダークラスのメソッドを含む呼び出しに変換されます。 コンピュテーション式は、 `{| cexpr |}` これらの翻訳に従って再帰的に定義されます。ここで、 `expr` は F # の式で、 `cexpr` はコンピュテーション式です。

|Expression|翻訳|
|----------|-----------|
|<code>{ let binding in cexpr }</code>|<code>let binding in {&#124; cexpr &#124;}</code>|
|<code>{ let! pattern = expr in cexpr }</code>|<code>builder.Bind(expr, (fun pattern -> {&#124; cexpr &#124;}))</code>|
|<code>{ do! expr in cexpr }</code>|<code>builder.Bind(expr, (fun () -> {&#124; cexpr &#124;}))</code>|
|<code>{ yield expr }</code>|`builder.Yield(expr)`|
|<code>{ yield! expr }</code>|`builder.YieldFrom(expr)`|
|<code>{ return expr }</code>|`builder.Return(expr)`|
|<code>{ return! expr }</code>|`builder.ReturnFrom(expr)`|
|<code>{ use pattern = expr in cexpr }</code>|<code>builder.Using(expr, (fun pattern -> {&#124; cexpr &#124;}))</code>|
|<code>{ use! value = expr in cexpr }</code>|<code>builder.Bind(expr, (fun value -> builder.Using(value, (fun value -> { cexpr }))))</code>|
|<code>{ if expr then cexpr0 &#124;}</code>|<code>if expr then { cexpr0 } else builder.Zero()</code>|
|<code>{ if expr then cexpr0 else cexpr1 &#124;}</code>|<code>if expr then { cexpr0 } else { cexpr1 }</code>|
|<code>{ match expr with &#124; pattern_i -> cexpr_i }</code>|<code>match expr with &#124; pattern_i -> { cexpr_i }</code>|
|<code>{ for pattern in expr do cexpr }</code>|<code>builder.For(enumeration, (fun pattern -> { cexpr }))</code>|
|<code>{ for identifier = expr1 to expr2 do cexpr }</code>|<code>builder.For(enumeration, (fun identifier -> { cexpr }))</code>|
|<code>{ while expr do cexpr }</code>|<code>builder.While(fun () -> expr, builder.Delay({ cexpr }))</code>|
|<code>{ try cexpr with &#124; pattern_i -> expr_i }</code>|<code>builder.TryWith(builder.Delay({ cexpr }), (fun value -> match value with &#124; pattern_i -> expr_i &#124; exn -> reraise exn)))</code>|
|<code>{ try cexpr finally expr }</code>|<code>builder.TryFinally(builder.Delay( { cexpr }), (fun () -> expr))</code>|
|<code>{ cexpr1; cexpr2 }</code>|<code>builder.Combine({ cexpr1 }, { cexpr2 })</code>|
|<code>{ other-expr; cexpr }</code>|<code>expr; { cexpr }</code>|
|<code>{ other-expr }</code>|`expr; builder.Zero()`|

前の表では、 `other-expr` 表に記載されていない式について説明します。 ビルダークラスは、すべてのメソッドを実装する必要はなく、前の表に一覧表示されているすべての変換をサポートします。 実装されていない構造体は、その型のコンピュテーション式では使用できません。 たとえば、計算式でキーワードをサポートしない場合は、 `use` の定義を `Use` ビルダークラスで省略できます。

次のコード例は、一度に1ステップずつ評価できる一連のステップとして計算をカプセル化するコンピュテーション式を示しています。 判別共用体型は、これ `OkOrException` までに評価された式のエラー状態をエンコードします。 このコードは、いくつかのビルダーメソッドの定型的な実装など、計算式で使用できる一般的なパターンを示しています。

```fsharp
// Computations that can be run step by step
type Eventually<'T> =
    | Done of 'T
    | NotYetDone of (unit -> Eventually<'T>)

module Eventually =
    // The bind for the computations. Append 'func' to the
    // computation.
    let rec bind func expr =
        match expr with
        | Done value -> func value
        | NotYetDone work -> NotYetDone (fun () -> bind func (work()))

    // Return the final value wrapped in the Eventually type.
    let result value = Done value

    type OkOrException<'T> =
        | Ok of 'T
        | Exception of System.Exception

    // The catch for the computations. Stitch try/with throughout
    // the computation, and return the overall result as an OkOrException.
    let rec catch expr =
        match expr with
        | Done value -> result (Ok value)
        | NotYetDone work ->
            NotYetDone (fun () ->
                let res = try Ok(work()) with | exn -> Exception exn
                match res with
                | Ok cont -> catch cont // note, a tailcall
                | Exception exn -> result (Exception exn))

    // The delay operator.
    let delay func = NotYetDone (fun () -> func())

    // The stepping action for the computations.
    let step expr =
        match expr with
        | Done _ -> expr
        | NotYetDone func -> func ()

    // The rest of the operations are boilerplate.
    // The tryFinally operator.
    // This is boilerplate in terms of "result", "catch", and "bind".
    let tryFinally expr compensation =
        catch (expr)
        |> bind (fun res ->
            compensation();
            match res with
            | Ok value -> result value
            | Exception exn -> raise exn)

    // The tryWith operator.
    // This is boilerplate in terms of "result", "catch", and "bind".
    let tryWith exn handler =
        catch exn
        |> bind (function Ok value -> result value | Exception exn -> handler exn)

    // The whileLoop operator.
    // This is boilerplate in terms of "result" and "bind".
    let rec whileLoop pred body =
        if pred() then body |> bind (fun _ -> whileLoop pred body)
        else result ()

    // The sequential composition operator.
    // This is boilerplate in terms of "result" and "bind".
    let combine expr1 expr2 =
        expr1 |> bind (fun () -> expr2)

    // The using operator.
    let using (resource: #System.IDisposable) func =
        tryFinally (func resource) (fun () -> resource.Dispose())

    // The forLoop operator.
    // This is boilerplate in terms of "catch", "result", and "bind".
    let forLoop (collection:seq<_>) func =
        let ie = collection.GetEnumerator()
        tryFinally
            (whileLoop
                (fun () -> ie.MoveNext())
                (delay (fun () -> let value = ie.Current in func value)))
            (fun () -> ie.Dispose())

// The builder class.
type EventuallyBuilder() =
    member x.Bind(comp, func) = Eventually.bind func comp
    member x.Return(value) = Eventually.result value
    member x.ReturnFrom(value) = value
    member x.Combine(expr1, expr2) = Eventually.combine expr1 expr2
    member x.Delay(func) = Eventually.delay func
    member x.Zero() = Eventually.result ()
    member x.TryWith(expr, handler) = Eventually.tryWith expr handler
    member x.TryFinally(expr, compensation) = Eventually.tryFinally expr compensation
    member x.For(coll:seq<_>, func) = Eventually.forLoop coll func
    member x.Using(resource, expr) = Eventually.using resource expr

let eventually = new EventuallyBuilder()

let comp = eventually {
    for x in 1..2 do
        printfn " x = %d" x
    return 3 + 4 }

// Try the remaining lines in F# interactive to see how this
// computation expression works in practice.
let step x = Eventually.step x

// returns "NotYetDone <closure>"
comp |> step

// prints "x = 1"
// returns "NotYetDone <closure>"
comp |> step |> step

// prints "x = 1"
// prints "x = 2"
// returns "Done 7"
comp |> step |> step |> step |> step
```

コンピュテーション式には基になる型があり、この式はを返します。 基になる型は、計算された結果または実行可能な遅延計算を表すことができます。また、一部の型のコレクションを反復処理する方法を提供する場合もあります。 前の例では、基になる型は **最終的**にでした。 シーケンス式の場合、基になる型は <xref:System.Collections.Generic.IEnumerable%601?displayProperty=nameWithType> です。 クエリ式の場合、基になる型は <xref:System.Linq.IQueryable?displayProperty=nameWithType> です。 非同期ワークフローの場合、基になる型は [`Async`](https://fsharp.github.io/fsharp-core-docs/reference/fsharp-control-fsharpasync-1.html) です。 オブジェクトは、 `Async` 結果を計算するために実行する作業を表します。 たとえば、 [`Async.RunSynchronously`](https://fsharp.github.io/fsharp-core-docs/reference/fsharp-control-fsharpasync.html#RunSynchronously) を呼び出して計算を実行し、その結果を返します。

## <a name="custom-operations"></a>カスタム操作

コンピュテーション式でカスタム操作を定義し、コンピュテーション式で演算子としてカスタム操作を使用することができます。 たとえば、クエリ式にクエリ演算子を含めることができます。 カスタム操作を定義する場合は、コンピュテーション式の Yield メソッドと For メソッドを定義する必要があります。 カスタム操作を定義するには、それをコンピュテーション式のビルダークラスに配置し、を適用し [`CustomOperationAttribute`](https://fsharp.github.io/fsharp-core-docs/reference/fsharp-core-customoperationattribute.html) ます。 この属性は、引数として文字列を受け取ります。これはカスタム操作で使用される名前です。 この名前は、コンピュテーション式の左中かっこの先頭にあるスコープに含まれます。 したがって、このブロック内のカスタム操作と同じ名前を持つ識別子は使用しないでください。 たとえば、 `all` クエリ式でやなどの識別子を使用しないようにし `last` ます。

### <a name="extending-existing-builders-with-new-custom-operations"></a>新しいカスタム操作を使用した既存のビルダーの拡張

既にビルダークラスがある場合は、そのカスタム操作をこのビルダークラスの外部から拡張できます。 拡張機能はモジュール内で宣言する必要があります。 名前空間には、同じファイルと、型が定義されている同じ名前空間宣言グループ以外の拡張メンバーを含めることはできません。

次の例は、既存のクラスの拡張を示して `FSharp.Linq.QueryBuilder` います。

```fsharp
type FSharp.Linq.QueryBuilder with

    [<CustomOperation("existsNot")>]
    member _.ExistsNot (source: QuerySource<'T, 'Q>, predicate) =
        Enumerable.Any (source.Source, Func<_,_>(predicate)) |> not
```

## <a name="see-also"></a>関連項目

- [F# 言語リファレンス](index.md)
- [非同期ワークフロー](asynchronous-workflows.md)
- [シーケンス](sequences.md)
- [クエリ式](query-expressions.md)
